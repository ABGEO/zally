apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'checkstyle'
apply plugin: 'pmd'
apply plugin: 'com.github.johnrengelman.shadow'

repositories {
    mavenCentral()
}

dependencies {
    compile 'com.github.ryenus:rop:1.1.1'
    compile 'com.mashape.unirest:unirest-java:1.4.9'
    compile 'org.yaml:snakeyaml:1.17'
    compile 'org.json:json:20160810'

    testCompile 'junit:junit:4.12'
    testCompile 'net.jadler:jadler-core:1.3.0'
    testCompile 'net.jadler:jadler-jetty:1.3.0'
    testCompile 'net.jadler:jadler-junit:1.3.0'
    testCompile 'org.mockito:mockito-core:2.5.0'
}

def versions = [
  checkstyle: '7.4',
  pmd: '5.5.2',
  jacoco: '0.7.8',
]

configurations {
  checkstyleConfig
  checkstyleConfig.transitive = false
}

dependencies {
  checkstyleConfig "com.puppycrawl.tools:checkstyle:${versions.checkstyle}"
}

checkstyle {
  toolVersion = versions.checkstyle
}

pmd {
  toolVersion = versions.pmd
}

jacoco {
    toolVersion = versions.jacoco
}

buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.3'
    }
}

jar {
   manifest {
       attributes 'Main-Class': 'de.zalando.zally.cli.Main'
   }
}

shadowJar {
   destinationDir = file('releases')
   baseName = 'zally'
   classifier = null
}

jacocoTestReport {
  reports {
    xml.enabled true
  }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.14.1'
}

task copyToBin << {
  copy {
    from "${shadowJar.outputs.getFiles().getFiles()[0]}"
    into 'bin'
    rename { String fileName -> "zally.jar" }
  }
}

copyToBin.dependsOn shadowJar
build.dependsOn copyToBin
check.dependsOn jacocoTestReport
