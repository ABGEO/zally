configurations {
    ktlint
}

def versions = [
  jacoco: '0.7.8',
]

buildscript {

    ext.kotlin_version = '1.0.6'

    repositories {
        mavenCentral()
        jcenter()
        maven { url "https://repo.spring.io/libs-release" }
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.4.3.RELEASE")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version")
        classpath('com.github.ben-manes:gradle-versions-plugin:0.13.0')
    }
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'jacoco'
apply plugin: 'org.springframework.boot'
apply plugin: 'com.github.ben-manes.versions'

task wrapper(type: Wrapper) {
    gradleVersion = '2.14.1'
}

jar {
    baseName = 'zally'
    version =  '0.0.1'
}

repositories {
    jcenter()
    mavenCentral()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    compile("io.swagger:swagger-parser:1.0.25")
    compile("org.springframework.boot:spring-boot-starter-web")
    compile("org.springframework.boot:spring-boot-starter-actuator")
    compile("org.springframework.boot:spring-boot-starter-freemarker")
    compile("org.zalando.stups:stups-spring-oauth2-server:1.0.16")
    compile("org.zalando:problem-spring-web:0.16.0")
    compile("org.zalando:twintip-spring-web:1.1.0")
    compile("org.zalando.zmon:zmon-actuator:0.9.7")
    compile("io.dropwizard.metrics:metrics-core:3.1.0")
    compile("com.fasterxml.jackson.datatype:jackson-datatype-jdk8:2.8.5")
    compile("com.fasterxml.jackson.module:jackson-module-kotlin:2.8.6")
    compile("org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version")
    testCompile("org.springframework.boot:spring-boot-starter-test")
    testCompile("org.assertj:assertj-core:3.6.1")

    ktlint("com.github.shyiko:ktlint:0.3.0")
}

jacoco {
    toolVersion = versions.jacoco
}

jacocoTestReport {
  reports {
    xml.enabled true
  }
}

task ktlint(type: JavaExec) {
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    args "src/**/*.kt"
}

check.dependsOn jacocoTestReport
check.dependsOn ktlint
